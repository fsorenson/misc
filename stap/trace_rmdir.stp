/* Frank Sorenson <sorenson@redhat.com>, 2024 */

function tstamp:string(epochns:long) %{ /* pure */
	u64 nsec = STAP_ARG_epochns;
	time_t epochsec = nsec / 1000000000;
	struct tm gmt;

	nsec = nsec % 1000000000;
	time_to_tm(epochsec, -tz_gmtoff, &gmt);
	snprintf(STAP_RETVALUE, MAXSTRINGLEN, "%04ld-%02d-%02d %02d:%02d:%02d.%09llu",
		gmt.tm_year + 1900, gmt.tm_mon + 1, gmt.tm_mday,
		gmt.tm_hour, gmt.tm_min, gmt.tm_sec, nsec);
%}
probe kernel.function("do_rmdir") {
	pathname = user_string($pathname)

	if (substr(pathname, 0, 1) != "/") {
		cwd_path = &@cast(task_cwd_path(task_current()), "struct path")
		cwd_path_str = task_dentry_path(task_current(), cwd_path->dentry, cwd_path->mnt)

		pathname = cwd_path_str . "/" . pathname
	}

	pos = strpos(pathname, "WATCHED_PATH")
	if (pos != -1)
		printf("%s: %s(%d) rmdir(\"%s\") - (%s)\n",
			tstamp(gettimeofday_ns()), execname(), tid(), pathname, task_ancestry(task_current(), 0))
}
